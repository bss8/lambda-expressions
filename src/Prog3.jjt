/**
 *
 * Copyright (c) 1996-1997 Sun Microsystems, Inc.
 *
 * Use of this file and the system it is part of is constrained by the
 * file COPYRIGHT in the root directory of this system.
 *
 */

options {
  MULTI=true; // an option for JJTree
  STATIC=false;// set to build a multi mode tree
              // (with different types of nodes)

}

PARSER_BEGIN(Prog3)

class Prog3 {
  public static void main(String[] args) {
    System.out.println(">>> Lambda Expression Evaluator <<<");
	System.out.println("Enter an applied Lambda expression:");
    Prog3 x = new Prog3(System.in);
    try {
      ASTStart z = x.Start();
	  System.out.println("The abstract syntax tree:");
      z.dump("");
	  System.out.println("");
	  System.out.println("The Lambda expression in the concrete syntax:");
	  System.out.println(z.printExpr());
	  System.out.println("");
	  System.out.println("The abstract syntax tree with free variables:");
	  z.dumpFV("");
    } catch (Exception e) {
      System.out.println("Error:");
      System.out.println(e.getMessage());
      e.printStackTrace();
    }
  }
}

PARSER_END(Prog3)


SKIP :
{
     " "
    | "\t"
    | "\n"
    | "\r"
    | <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
    | <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

TOKEN :
{
        <VAR: <LETTER> (<LETTER>|<DIGIT>)* >
    |
      < #LETTER: ["_","a"-"z"] >
    |
      < #DIGIT: ["0"-"9"] >
    |
        <CONSTANTS: "*"|"+"|"-"|"/"|(["0"-"9"])+>
}

ASTStart Start() : {}
{
  Expression()
  { return jjtThis; }
}

void Expression() #void : {}
{ 
    constant() |
    var() | 
	"(" Branch() ")"
}

void Branch() #void : {}
{
	(Appl() | Lambda())
}

void Appl() #void : {}
{
    ( Expression() (Expression() #Appl(2))* )
}

void Lambda() #void : {}
{
    "L" body()
}

void body() #void : {}
{
 (var() (body() #Lambda(2))) | 
    "."  Appl()
}

void var() #var : { Token t; }
{ 
	t=<VAR>
	{
	  jjtThis.set(t.image);
	}
}

void constant() #constant : { Token t; }
{ 
	t=<CONSTANTS>
	{
	  jjtThis.set(t.image);
	}
}
